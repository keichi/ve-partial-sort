set term pdfcairo enhanced size 4in, 3in font "Helvetical,10"

set datafile separator ","

set grid

# set style data line

set style line  1 lt 1 pt  1 lc rgb '#4E79A7'
set style line  2 lt 1 pt  2 lc rgb '#F28E2B'
set style line  3 lt 1 pt  3 lc rgb '#E15759'
set style line  4 lt 1 pt  4 lc rgb '#76B7B2'
set style line  5 lt 1 pt  5 lc rgb '#59A14E'
set style line  6 lt 1 pt  6 lc rgb '#EDC949'
set style line  7 lt 1 pt  7 lc rgb '#B07AA2'
set style line  8 lt 1 pt  8 lc rgb '#FF9DA7'
set style line  9 lt 1 pt  9 lc rgb '#9C755F'
set style line 10 lt 1 pt 10 lc rgb '#BAB0AC'

set key box width 2 top left

set logscale xy
set xlabel "Length of vector"
set ylabel "Runtime [ms]"

set output "n_var_k_1.pdf"

plot "performance.csv" every 8 using 1:5 with lp ls 3 title "STL full sort", \
                    "" every 8 using 1:6 with lp ls 4 title "STL partial sort", \
                    "" every 8 using 1:3 with lp ls 1 title "ASL full sort", \
                    "" every 8 using 1:4 with lp ls 2 title "LSD radix full sort", \
                    "" every 8 using 1:7 with lp ls 5 title "MSD radix partial sort"

set logscale x
set xlabel "Number of neighbors"
set ylabel "Runtime [ms]"
set yrange [*:2]

set output "n_10k_k_var.pdf"

plot "performance.csv" every ::22::28 using 2:5 with lp ls 3 title "STL full sort", \
                    "" every ::22::28 using 2:3 with lp ls 1 title "ASL full sort", \
                    "" every ::22::28 using 2:4 with lp ls 2 title "LSD radix full sort", \
                    "" every ::22::28 using 2:6 with lp ls 4 title "STL partial sort", \
                    "" every ::22::28 using 2:7 with lp ls 5 title "MSD radix partial sort"

set yrange [*:*]

set output "n_1m_k_var.pdf"

plot "performance.csv" every ::64::70 using 2:5 with lp ls 3 title "STL full sort", \
                    "" every ::64::70 using 2:3 with lp ls 1 title "ASL full sort", \
                    "" every ::64::70 using 2:4 with lp ls 2 title "LSD radix full sort", \
                    "" every ::64::70 using 2:6 with lp ls 4 title "STL partial sort", \
                    "" every ::64::70 using 2:7 with lp ls 5 title "MSD radix partial sort"
